#compdef eix-layman
local curcontext="$curcontext" state state_descr line
typeset -A opt_args
_arguments -C -s -S -A '-*' : \
'(1 -)'{'-h','-\\?'}'[help]' \
'*-a+[OVERLAY (add to list of layman overlays)]:add overlay:_files' \
'*-A+[PATH (add to list of sourced make.conf)]:add make.conf:_files' \
'-C[clear list of PATH of sourced make.conf files]' \
'-c+[LAYMAN_CFG (use as layman.cfg to define make.conf)]:layman.cfg-file:_files' \
'-v[verbose (default)]' \
'-H[no status line update]' \
'-q[quiet]' \
'*:commands:->cmds'
local ret=$?
case $state in
(cmds)
	local cmds
	cmds=(
	'add:Add the local layman overlays to the eix database'
	'remove:Remove the local layman overlays from the eix database'
	)
	_describe -t commands 'command' cmds && ret=0;;
esac
return ret
